apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.crakac.ofutodon"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            def p = getProps("mobile/debug_token.properties")
            p.each { k, v ->
                def val = v.toString()
                if (val.startsWith("\"")) {
                    buildConfigField "String", k, v
                } else if (val.contains(".")) {
                    buildConfigField "double", k, v
                } else if (val.endsWith("L")) {
                    buildConfigField "long", k, v
                } else {
                    buildConfigField "int", k, v
                }
            }
        }
    }
}

static def getProps(String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File(key)))
    return props
}


dependencies {
    ext {
        support_library_version = '26.1.0'
        firebase_version = '11.4.0'
        glide_version = '3.8.0'
        butterknife_version = '8.8.1'
        retrofit_version = '2.3.0'
        okhttp_version = '3.8.1'
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:support-v4:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:palette-v7:$support_library_version"
    compile "com.android.support:support-annotations:$support_library_version"
    compile "com.github.bumptech.glide:glide:$glide_version"
    compile "com.jakewharton:butterknife:$butterknife_version"
    kapt "com.jakewharton:butterknife-compiler:$butterknife_version"
    compile "com.google.firebase:firebase-core:$firebase_version"
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.okhttp3:okhttp:$okhttp_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    compile 'jp.wasabeef:glide-transformations:2.0.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    testCompile "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testCompile 'junit:junit:4.12'
    compile 'com.github.chrisbanes:PhotoView:2.1.3'

}

repositories {
    mavenCentral()
}

// this line must be bottom of file
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android-extensions'