apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.crakac.ofutodon"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            def p = getProps("mobile/debug_token.properties")
            p.each { k, v ->
                def val = v.toString()
                if (val.startsWith("\"")) {
                    buildConfigField "String", k, v
                } else if (val.endsWith('f')){
                    buildConfigField "float", k, v
                } else if (val.contains(".")) {
                    buildConfigField "double", k, v
                } else if (val.endsWith("L")) {
                    buildConfigField "long", k, v
                } else {
                    buildConfigField "int", k, v
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


def getProps(String file) {
    def propFile = rootProject.file(file)
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    return props
}



dependencies {
    ext {
        support_library_version = '27.1.0'
        firebase_version = '12.0.0'
        glide_version = '3.8.0'
        retrofit_version = '2.3.0'
        okhttp_version = '3.8.1'
        archlib_version = '1.0.0'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:support-v4:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:palette-v7:$support_library_version"
    implementation "com.android.support:support-annotations:$support_library_version"
    implementation "com.android.support:exifinterface:$support_library_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation 'jp.wasabeef:glide-transformations:3.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofit_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
}

repositories {
    mavenCentral()
}

// this line must be bottom of file
apply plugin: 'com.google.gms.google-services'