apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.crakac.ofutodon"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            def p = getProps("mobile/debug_token.properties")
            p.each {k, v ->
                def val = v.toString()
                if(val.startsWith("\"")) {
                    buildConfigField "String", k, v
                } else if (val.contains(".")) {
                    buildConfigField "double", k, v
                } else if (val.endsWith("L")){
                    buildConfigField "long", k, v
                } else {
                    buildConfigField "int", k, v
                }
            }
        }
    }
}

static def getProps(String key) {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File(key)))
    return props
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile "com.android.support:support-annotations:25.3.1"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    testCompile 'com.squareup.retrofit2:retrofit-mock:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.7.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.7.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.jakewharton:butterknife:8.5.1'
    kapt 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
}

kapt {
    generateStubs = true
}

repositories {
    mavenCentral()
}

// this line must be bottom of file
apply plugin: 'com.google.gms.google-services'